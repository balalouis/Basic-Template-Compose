plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'org.sonarqube'
}

android {
    namespace 'com.basic.template.compose'
    compileSdk 34

    defaultConfig {
        applicationId "com.basic.template.compose"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.basic.template.compose.hilt.HiltTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.8'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation project(path: ':network')
    implementation project(path: ':room')
    implementation project(path: ':login')
    implementation project(path: ':registration')
    implementation project(path: ':userlist')
    implementation project(path: ':userdetail')

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')

    setComposeDependency()
    setHiltDependency()
    setCoroutinesDependency()
    setNavigationDependency()
    setRoomDependency()
    setSquareDependency()
    setUITestingDependency()
    setLeakCanaryDependency()
    setCoilDependency()
    setUnitTestingDependency()
}

private void setComposeDependency() {
    dependencies.implementation 'androidx.activity:activity-compose:1.8.2'
    dependencies.implementation 'androidx.compose.ui:ui'
    dependencies.implementation 'androidx.compose.ui:ui-graphics'
    dependencies.implementation 'androidx.compose.ui:ui-tooling-preview'
    dependencies.implementation 'androidx.compose.material3:material3'
    dependencies.androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    dependencies.debugImplementation 'androidx.compose.ui:ui-tooling'
    dependencies.debugImplementation 'androidx.compose.ui:ui-test-manifest'
}

private void setHiltDependency() {
    dependencies.implementation "com.google.dagger:hilt-android:2.50"
    dependencies.kapt "com.google.dagger:hilt-compiler:2.50"
    dependencies.implementation 'androidx.hilt:hilt-navigation-compose:1.2.0'
    // For instrumented tests.
    dependencies.androidTestImplementation 'com.google.dagger:hilt-android-testing:2.50'
    // ...with Kotlin.
    dependencies.kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.50'
}

private void setCoroutinesDependency() {
    def coroutines_test_version = '1.6.1'
    // coroutines for test case
    dependencies.testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_version"
    // optional - Kotlin Extensions and Coroutines support for Room
    dependencies.implementation('androidx.room:room-ktx:2.6.1')
}

private void setNavigationDependency() {
    def nav_version = "2.7.7"
    dependencies.implementation "androidx.navigation:navigation-compose:$nav_version"
    dependencies.androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
}

private void setUITestingDependency() {
    def cucumber_javaVersion = '7.14.0'
    dependencies.androidTestImplementation "io.cucumber:cucumber-android:$cucumber_javaVersion"
    dependencies.androidTestImplementation "io.cucumber:cucumber-android-hilt:$cucumber_javaVersion"
    // Espresso
    dependencies.implementation 'androidx.test.espresso:espresso-idling-resource:3.5.1'
    dependencies.androidTestImplementation("com.jakewharton.espresso:okhttp3-idling-resource:1.0.0")

}

private void setUnitTestingDependency() {
    dependencies.testImplementation 'junit:junit:4.13.2'
    dependencies.androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    dependencies.androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

private void setRoomDependency() {
    // Room
    dependencies.implementation('androidx.room:room-runtime:2.6.1')
    dependencies.annotationProcessor('androidx.room:room-compiler:2.6.1')

    // To use Kotlin annotation processing tool (kapt)
    dependencies.kapt('androidx.room:room-compiler:2.6.1')
}

private void setSquareDependency() {
    // Mock web server
    dependencies.testImplementation("com.squareup.okhttp3:mockwebserver:4.12.0")
    dependencies.androidTestImplementation("com.squareup.okhttp3:mockwebserver:4.12.0")
    dependencies.implementation("com.squareup.okhttp3:okhttp:4.12.0")
    dependencies.implementation("com.squareup.okhttp3:logging-interceptor:4.10.0")

    // Retrofit
    dependencies.implementation "com.squareup.retrofit2:retrofit:2.9.0"
    dependencies.implementation "com.squareup.retrofit2:converter-gson:2.9.0"
}

private void setLeakCanaryDependency() {
    // Leak canary
    dependencies.debugImplementation("com.squareup.leakcanary:leakcanary-android:2.12")
    dependencies.androidTestImplementation("com.squareup.leakcanary:leakcanary-android-instrumentation:2.12")
}

private void setCoilDependency() {
    // Coil
    dependencies.implementation("io.coil-kt:coil-compose:2.4.0")
}